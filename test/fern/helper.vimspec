Describe fern#helper
  After all
    %bwipeout!
  End

  Before
    %bwipeout!
    let TIMEOUT = 5000
    let STATUS_EXPANDED = g:fern#internal#node#STATUS_EXPANDED
    let Promise = vital#fern#import('Async.Promise')
    let provider = fern#scheme#debug#provider#new()
    let b:fern = fern#internal#core#new('debug:///', provider)

    let sync_methods = [
          \ 'get_root_node',
          \ 'get_cursor_node',
          \ 'get_marked_nodes',
          \ 'get_selected_nodes',
          \ 'get_cursor',
          \ 'set_cursor',
          \ 'save_cursor',
          \ 'restore_cursor',
          \ 'cancel',
          \ 'process_node',
          \]
    let async_methods = [
          \ 'sleep',
          \ 'redraw',
          \ 'update_nodes',
          \ 'update_marks',
          \ 'expand_node',
          \ 'collapse_node',
          \ 'reload_node',
          \ 'reveal_node',
          \ 'focus_node',
          \ 'set_mark',
          \ 'set_hidden',
          \ 'set_include',
          \ 'set_exclude',
          \ 'enter_tree',
          \ 'leave_tree',
          \]
  End

  Describe #new()
    It returns a new helper instance
      let helper = fern#helper#new()
      Assert KeyExists(helper, 'fern')
      Assert KeyExists(helper, 'bufnr')
      Assert KeyExists(helper, 'winid')
      Assert KeyExists(helper, 'STATUS_NONE')
      Assert KeyExists(helper, 'STATUS_COLLAPSED')
      Assert KeyExists(helper, 'STATUS_EXPANDED')

      for sync_method in sync_methods
        Assert KeyExists(helper, sync_method)
        Assert IsFunction(helper[sync_method])
      endfor

      for async_method in async_methods
        Assert KeyExists(helper, async_method)
        Assert IsFunction(helper[async_method])
      endfor
    End
  End

  Describe a helper instance
    Before
      let helper = fern#helper#new()
    End

    Describe Synchronous methods
      Describe .get_root_node()
        It returns a root node
          Assert Equals(helper.sync.get_root_node(), helper.fern.root)
        End
      End

      Describe .get_cursor_node()
        It returns a cursor node
          Assert Equals(helper.sync.get_cursor_node(), helper.fern.root)
        End
      End

      Describe .get_marked_nodes()
        It returns marked nodes
          Assert Equals(helper.sync.get_marked_nodes(), [])

          let helper.async.set_mark([], 1)
          Assert Equals(helper.sync.get_marked_nodes(), [helper.fern.root])
        End
      End

      Describe .get_selected_nodes()
        It returns marked nodes if at least one node has marked
          let helper.async.set_mark([], 1)
          Assert Equals(helper.sync.get_selected_nodes(), helper.sync.get_marked_nodes())
        End

        It returns a cursor node in list if no node has marked
          Assert Equals(helper.sync.get_selected_nodes(), [helper.sync.get_cursor_node()])
        End
      End

      Describe .get_cursor()
        It returns a [line, col - 1] list
          Assert Equals(helper.sync.get_cursor(), [line('.'), col('.') - 1])
        End
      End

      Describe .set_cursor()
        It move cursor position
          call setline(1, ["Hello", "World"])
          call helper.sync.set_cursor([2, 4])
          Assert Equals(line('.'), 2)
          Assert Equals(col('.'), 5)
        End
      End

      Describe .save_cursor()
        It does not raise exception
          call helper.sync.save_cursor()
        End
      End

      Describe .restore_cursor()
        It does not raise exception
          call helper.sync.restore_cursor()
        End
      End

      Describe .cancel()
        It does not raise exception
          call helper.sync.cancel()
        End
      End

      Describe .process_node()
        It does not raise exception
          let root = helper.sync.get_root_node()
          call helper.sync.process_node(root)
        End

        It returns a function
          let root = helper.sync.get_root_node()
          Assert IsFunction(helper.sync.process_node(root))
        End
      End

      Describe .is_drawer()
        It does not raise exception
          call helper.sync.is_drawer()
        End

        It returns a boolean
          Assert IsNumber(helper.sync.is_drawer())
        End

        It returns 1 if the fern is shown in a project drawer
          Fern debug:/// -drawer
          let helper = fern#helper#new()
          Assert True(helper.sync.is_drawer())
        End

        It returns 0 if the fern is NOT shown in a project drawer
          Fern debug:///
          let helper = fern#helper#new()
          Assert False(helper.sync.is_drawer())
        End
      End

      Describe .get_scheme()
        It does not raise exception
          call helper.sync.get_scheme()
        End

        It returns a string
          Assert IsString(helper.sync.get_scheme())
        End

        It returns a scheme name of a fern buffer
          Fern debug:///
          let helper = fern#helper#new()
          Assert Equals(helper.sync.get_scheme(), 'debug')

          Fern dict:///
          let helper = fern#helper#new()
          Assert Equals(helper.sync.get_scheme(), 'dict')

          Fern file:///
          let helper = fern#helper#new()
          Assert Equals(helper.sync.get_scheme(), 'file')
        End
      End
    End

    Describe Asynchronous methods
      Describe .sleep()
        It returns a promise
          let p = helper.async.sleep(1)
          Assert True(Promise.is_promise(p))
        End

        It does not reject the promise
          let [r, e] = Promise.wait(
                \ helper.async.sleep(1),
                \ { 'timoeut': TIMEOUT },
                \)
          Assert Equals(e, v:null)
        End
      End

      Describe .redraw()
        It returns a promise
          let p = helper.async.redraw()
          Assert True(Promise.is_promise(p))
        End

        It does not reject the promise
          let [r, e] = Promise.wait(
                \ helper.async.redraw(),
                \ { 'timoeut': TIMEOUT },
                \)
          Assert Equals(e, v:null)
        End
      End

      " TODO Write tests
    End
  End
End
